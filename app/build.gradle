plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
}

// .env 파일 로딩 함수
def loadEnvIntoBootRunEnvironment(String path = ".env") {
	def envFile = rootProject.file(path)
	if (!envFile.exists()) {
		println ".env file not found: $path"
		return [:]
	}

	def envVars = [:]
	envFile.eachLine { line ->
		line = line.trim()
		if (!line || line.startsWith("#")) return

		def (key, value) = line.split("=", 2)
		key = key.trim()
		value = value.trim().replaceAll('^"|"$', '') // 양쪽 큰따옴표 제거
		envVars[key] = value
	}
	return envVars
}

bootRun {
	// .env 파일에서 환경 변수 읽어와 설정
	environment loadEnvIntoBootRunEnvironment()

	// 필요 시 인코딩 설정
	jvmArgs = ["-Dfile.encoding=UTF-8"]
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
    mavenCentral() // https://repo.maven.apache.org/maven2/
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


tasks.named('test') {
    useJUnitPlatform()
}

// 마이크로서비스 + Docker 기반 배포용
// 기존의 group, version은 무시된다.
tasks.named('bootJar') {
    archiveBaseName.set(rootProject.name)     // 기본 이름
    archiveVersion.set('')      // 버전
    archiveClassifier.set('') // 접미사
}

